/*
Copyright 2023 Google LLC
Licensed under the Apache License, Version 2.0 (the "License");
you may not use this file except in compliance with the License.
You may obtain a copy of the License at
https://www.apache.org/licenses/LICENSE-2.0
Unless required by applicable law or agreed to in writing, software
distributed under the License is distributed on an "AS IS" BASIS,
WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
See the License for the specific language governing permissions and
limitations under the License.
*/
rule GCP_Identity_Alert_Severity_Escalation {

meta:
author = "Google Cloud Security"
description = "Detects when an identity experiences a low severity event followed by medium severity event within 24 hours."
rule_name = "Escalated security alerts for an identity"
type = "Alert"
platform = "GCP"
data_source = "Security Command Center Threat"
//This specific detection rule is related to T1078 technique which falls under multiple tactics, including Initial Access, Persistence, Privilege Escalation, and Defense Evasion. To reflect its cross-tactic applicability, it has not been tied to just one specific tactic for this rule. 
mitre_attack_technique = "Valid Accounts"
mitre_attack_url = "https://attack.mitre.org/techniques/T1078/"
mitre_attack_version = "v16.0"
severity = "High"
priority = "High"

events:
// Low severity alert triggered
$low_severity.security_result[0].severity = "LOW"
$low_severity.principal.user.userid = $user_id

// Medium severity alert triggered
$medium_severity.security_result[0].severity = "MEDIUM"
$medium_severity.principal.user.userid = $user_id

// Enforces the sequence of events, ensuring the low severity event(s) triggered prior to the medium severity event(s)
$low_severity.metadata.event_timestamp.seconds < $medium_severity.metadata.event_timestamp.seconds

match:
$user_id over 24h

outcome:
$risk_score = 85
$security_category = array_distinct($medium_severity.security_result.category)
$security_category_details = array_distinct($medium_severity.security_result.category_details)
$security_summary = array_distinct($medium_severity.security_result.summary)
$affected_identity = array_distinct($medium_severity.principal.user.userid)
$principal_ip_country = array_distinct($medium_severity.principal.ip_geo_artifact.location.country_or_region)
$principal_ip_state = array_distinct($medium_severity.principal.ip_geo_artifact.location.state)
$event_count_low = count_distinct($low_severity.metadata.id)
$principal_user_display_name = array_distinct($medium_severity.principal.user.user_display_name)
$target_resource_name = $medium_severity.target.resource.name

condition:
$low_severity and $medium_severity
}